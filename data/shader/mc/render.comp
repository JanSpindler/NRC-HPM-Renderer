#version 460
#define MC
#include "common.glsl"

layout(local_size_x = 32, local_size_y = 1, local_size_z = 1) in;

vec4 TracePath(const ivec2 imageCoord, const vec3 rayOrigin, const vec3 rayDir, out bool didScatter, out vec3 firstVolumeHit)
{
	vec3 scatteredLight = vec3(0.0);

	const vec3 entry = find_entry_exit(rayOrigin, rayDir)[0];
	
	vec3 currentPoint = entry;
	vec3 currentDir = rayDir;
	
	float factor = 1.0;

	didScatter = false;
	bool volumeExit = false;

	for (int i = 0; i < PATH_LENGTH; i++)
	{
		// Find new point
		currentPoint = DeltaTrack(currentPoint, currentDir, volumeExit);
		if (volumeExit) { break; }
		didScatter = true;

		// Proper weighting of light
		factor *= 0.5; // * 0.5 because L_s is being approximated by 2 samples

		// Lighting
		const vec3 sceneLighting = TraceScene(currentPoint, currentDir) * factor;
		const float density = getDensity(currentPoint);
		scatteredLight += density * sceneLighting; // Phase and transmittance are IS

		// Find new dir by IS the PF
		currentDir = NewRayDir(currentDir, true);
	}

	return vec4(scatteredLight, factor);
}

void main()
{
	const uint x = gl_GlobalInvocationID.x;
	const uint y = gl_GlobalInvocationID.y;
	ivec2 imageCoord = ivec2(x, y);

	// Fraguv and world pos
	const vec2 fragUV = vec2(float(x) * ONE_OVER_RENDER_WIDTH, float(y) * ONE_OVER_RENDER_HEIGHT);
	const vec4 screenCoord = vec4((fragUV * 2.0) - vec2(1.0), 0.0, 1.0);
	const vec4 worldPos = camMat.invProjView * screenCoord;
	const vec3 pixelWorldPos = worldPos.xyz / worldPos.w;

	// Setup random
	InitRandom(fragUV);

	// Setup ray
	const vec3 ro = camera.pos;
	vec3 rd = normalize(pixelWorldPos - ro);

	// SDF + render
	const vec3[2] entry_exit = find_entry_exit(ro, rd);
	const vec3 entry = entry_exit[0];
	const vec3 exit = entry_exit[1];

	vec4 outputColor;
	bool didScatter = false;
	vec3 firstVolumeHit;
	if (sky_sdf(entry) > MAX_RAY_DISTANCE)
	{ 
		outputColor = vec4(SampleHdrEnvMap(rd), 1.0);
	}
	else
	{ 
		outputColor = TracePath(imageCoord, ro, rd, didScatter, firstVolumeHit);
		if (!didScatter) { outputColor = vec4(SampleHdrEnvMap(rd), 1.0); }
	}
	outputColor.w = didScatter ? 1.0 : 0.0;

	// Store output
	vec4 blendedVolumeColor = (blendFactor * outputColor) + ((1.0 - blendFactor) * imageLoad(outputImage, imageCoord));
	imageStore(outputImage, imageCoord, blendedVolumeColor);
	imageStore(infoImage, imageCoord, vec4(didScatter ? 1.0 : 0.0, 0.0, 0.0, 0.0));
}
